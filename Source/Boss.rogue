module Boss

$include "Macros.rogue"

$include "BossObject.rogue"
$include "BossValue.rogue"
$include "Candidates.rogue"
$include "Collector.rogue"
$include "CmdExecution.rogue"
$include "CmdTypes.rogue"
$include "Module.rogue"
$include "Organizer.rogue"
$include "Parser.rogue"
$include "Program.rogue"
$include "Resolver.rogue"
$include "Scanner.rogue"
$include "Type.rogue"
uses Boss
uses Utility/CommandLineParser

try
  Boss( System.command_line_arguments )

catch (error:CompileError)
  Console.error.println error
  System.exit 1
catch (error:Error)
  Console.error.println error
  Console.error.println error.stack_trace
  System.exit 1
endTry

class Boss
  PROPERTIES
    command : Value

  METHODS
    method init( args:String[] )
      command = parse_args( args )

      if (command//options//help or command//args.count == 0)
        print_usage
        System.exit 0
      endIf

      register_native_code

      parse_files

    method parse_files
      Program.include( forEach in command//args )
      Program.launch

    method parse_args( args:String[] )->Value
      # This method is unrelated to the Froley Parser
      local command = CommandLineParser().
      [
        option( "--flag",     &alias="-f" )
        option( "--help",     &aliases=["-h","-?"] )
        option( "--setting=", &alias="-s" )
      ].parse( args )
      return command

    method print_usage
      println @|USAGE
               |  boss [OPTIONS] <filepath>
               |
               |OPTIONS
               |  --help, -h, -?
               |    Show this help text.

    method register_native_code
      register_random

    method register_random
      Program.type("Random").is_singleton = true
      Program.type("Random").add_property( "native_random", "NativeObject" )
      Program.register( "Random.init",
        function( context:BossValue )->BossValue
          local obj = context->BossObject
          if (not obj) return Program.null_reference_error
          if (obj.properties.count) obj.properties[0] = BossValue( Random() )
          return true
        endFunction )
      Program.register( "Random.int32->Int32",
        function( context:BossValue )->BossValue
          local obj = context->BossObject
          if (not obj) return Program.null_reference_error
          local random = obj.properties[0].object->(as Random)
          if (not random) return Program.null_reference_error
          return random.int32
        endFunction )
      Program.register( "Random.int32(Int32)->Int32",
        function( context:BossValue, limit:BossValue )->BossValue
          local obj = context->BossObject
          if (not obj) return Program.null_reference_error
          local random = obj.properties[0].object->(as Random)
          if (not random) return Program.null_reference_error
          return random.int32(limit->Int32)
        endFunction )
      Program.register( "Random.int32(Int32,Int32)->Int32",
        function( context:BossValue, low:BossValue, high:BossValue )->BossValue
          local obj = context->BossObject
          if (not obj) return Program.null_reference_error
          local random = obj.properties[0].object->(as Random)
          if (not random) return Program.null_reference_error
          return random.int32( low->Int32, high->Int32 )
        endFunction )
      Program.register( "Random.logical->Logical",
        function( context:BossValue )->BossValue
          local obj = context->BossObject
          if (not obj) return Program.null_reference_error
          local random = obj.properties[0].object->(as Random)
          if (not random) return Program.null_reference_error
          return random.logical
        endFunction )
endClass

